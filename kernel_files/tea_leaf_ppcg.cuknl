
__global__ void device_tea_leaf_ppcg_solve_init_sd
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const r,
 const double * __restrict const Mi,
       double * __restrict const sd,
 double theta)
{
    __kernel_indexes;

    if (/*row >= (y_min + 1) - 0 && */row <= (y_max + 1) + 0
    && /*column >= (x_min + 1) - 0 &&*/ column <= (x_max + 1) + 0)
    {
#if defined(USE_PRECONDITIONER)
        sd[THARR2D(0, 0, 0)] = (Mi[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)])/theta;
#else
        sd[THARR2D(0, 0, 0)] = r[THARR2D(0, 0, 0)]/theta;
#endif
    }
}

__global__ void device_tea_leaf_ppcg_solve_update_r
(int x_min, int x_max, int y_min, int y_max,
       double * __restrict const u,
       double * __restrict const r,
 const double * __restrict const Kx,
 const double * __restrict const Ky,
       double * __restrict const sd)
{
    __kernel_indexes;

    if (/*row >= (y_min + 1) - 0 && */row <= (y_max + 1) + 0
    && /*column >= (x_min + 1) - 0 &&*/ column <= (x_max + 1) + 0)
    {
        const double result = (1.0
            + (Ky[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)])
            + (Kx[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]))*sd[THARR2D(0, 0, 0)]
            - (Ky[THARR2D(0, 1, 0)]*sd[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)]*sd[THARR2D(0, -1, 0)])
            - (Kx[THARR2D(1, 0, 0)]*sd[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]*sd[THARR2D(-1, 0, 0)]);

        r[THARR2D(0, 0, 0)] -= result;
        u[THARR2D(0, 0, 0)] += sd[THARR2D(0, 0, 0)];
    }
}

__global__ void device_tea_leaf_ppcg_solve_calc_sd
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const r,
 const double * __restrict const Mi,
       double * __restrict const sd,
 const double * __restrict const alpha,
 const double * __restrict const beta,
 int step)
{
    __kernel_indexes;

    if (/*row >= (y_min + 1) - 0 &&*/ row <= (y_max + 1) + 0
    && /*column >= (x_min + 1) - 0 &&*/ column <= (x_max + 1) + 0)
    {
        sd[THARR2D(0, 0, 0)] = alpha[step]*sd[THARR2D(0, 0, 0)]
// FIXME find out some way to do this in cuda - templates?
#if defined(USE_PRECONDITIONER)
#error Preconditioner does not yet work with ppcg solver - disable preconditioner in input file to run
                            + beta[step]*Mi[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)];
#else
                            + beta[step]*r[THARR2D(0, 0, 0)];
#endif
    }
}

__global__ void device_tea_leaf_ppcg_solve_init_p
(int x_min, int x_max, int y_min, int y_max,
       double * __restrict const p,
 const double * __restrict const r,
 const double * __restrict const Mi,
       double * __restrict const rro)
{
    __kernel_indexes;

    __shared__ double rro_shared[BLOCK_SZ];
    rro_shared[lid] = 0.0;

    if (/*row >= (y_min + 1) - 0 &&*/ row <= (y_max + 1) + 0
    && /*column >= (x_min + 1) - 0 &&*/ column <= (x_max + 1) + 0)
    {
#if defined(USE_PRECONDITIONER)
        p[THARR2D(0, 0, 0)] = Mi[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)];
#else
        p[THARR2D(0, 0, 0)] = r[THARR2D(0, 0, 0)];
#endif
        rro_shared[lid] = p[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)];
    }

    Reduce< double, BLOCK_SZ/2 >::run(rro_shared, rro, SUM);
}

