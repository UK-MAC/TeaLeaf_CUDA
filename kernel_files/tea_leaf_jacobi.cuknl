/*
 *  Kernels used for jacobi method
 */

__global__ void device_tea_leaf_jacobi_copy_u
(kernel_info_t kernel_info,
 const double * __restrict const u1,
       double * __restrict const un)
{
    __kernel_indexes;

    if (WITHIN_BOUNDS)
    {
        un[THARR2D(0, 0, 0)] = u1[THARR2D(0, 0, 0)];
    }
}

__global__ void device_tea_leaf_jacobi_solve
(kernel_info_t kernel_info,
       double * __restrict const Kx,
       double * __restrict const Ky,
 const double * __restrict const u0,
       double * __restrict const u1,
 const double * __restrict const un,
       double * __restrict const error)
{
    __kernel_indexes;

    __shared__ double error_local[BLOCK_SZ];
    error_local[lid] = 0;

    if (WITHIN_BOUNDS)
    {
        u1[THARR2D(0, 0, 0)] = (u0[THARR2D(0, 0, 0)]
            + Kx[THARR2D(1, 0, 0)]*un[THARR2D( 1,  0, 0)]
            + Kx[THARR2D(0, 0, 0)]*un[THARR2D(-1,  0, 0)]
            + Ky[THARR2D(0, 1, 0)]*un[THARR2D( 0,  1, 0)]
            + Ky[THARR2D(0, 0, 0)]*un[THARR2D( 0, -1, 0)])
            /(1.0 + (Kx[THARR2D(0, 0, 0)] + Kx[THARR2D(1, 0, 0)])
                  + (Ky[THARR2D(0, 0, 0)] + Ky[THARR2D(0, 1, 0)]));
        
        error_local[lid] = fabs(u1[THARR2D(0, 0, 0)] - un[THARR2D(0, 0, 0)]);
    }

    Reduce< double, BLOCK_SZ/2 >::run(error_local, error, SUM);
}


