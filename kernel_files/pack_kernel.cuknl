
/********************/

// j is column
// k is row

// could put this check in, prob doesnt need it
// if (row > 1 - depth && row < y_max + 2 + depth + y_extra)

// left/right buffer
// index=j+(k+depth-1)*depth

// left index 
// left_snd_buffer(index)=field(chunks(chunk)%field%x_min+x_extra-1+j,k)
// field(chunks(chunk)%field%x_min-j,k)=left_rcv_buffer(index)

// right index
// right_snd_buffer(index)=field(chunks(chunk)%field%x_max+1-j,k)
// field(chunks(chunk)%field%x_max+x_extra+j,k)=right_rcv_buffer(index)

/********************/

// top/bottom buffer
// index=j+depth+(k-1)*(chunks(chunk)%field%x_max+x_extra+(2*depth))

// bottom index
// bottom_snd_buffer(index)=field(j,chunks(chunk)%field%y_min+y_extra-1+k)
// field(j,chunks(chunk)%field%y_min-k)=bottom_rcv_buffer(index)

// top index
// top_snd_buffer(index)=field(j,chunks(chunk)%field%y_max+1-k)
// field(j,chunks(chunk)%field%y_max+y_extra+k)=top_rcv_buffer(index)

/********************/

// for top/bottom
#define HORZ_IDX(add) (column + depth + ((add + 1) - 1)*((x_max + 1) + x_extra + (2 * depth)) - 2)
// for left/right
#define VERT_IDX(add) ((add) + (row + depth - 1)*depth - 2)

__global__ void device_packleftBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
const double* array,
      double* left_buffer,
const int depth)
{
    int x_extra = type.x_extra;
    int y_extra = type.y_extra;

    //__kernel_indexes;
    const int glob_id = threadIdx.x + blockIdx.x * blockDim.x;
    const int row = glob_id / depth;
    const int column = glob_id % depth;

    if (row >= (y_min + 1) - depth && row <= (y_max + 1) + y_extra + depth)
    {
        const int row_begin = row * (x_max + 4 + x_extra);

        left_buffer[VERT_IDX(column)] = array[row_begin + (x_min + 1) + x_extra - 1 + (1 + column)];
    }
}

__global__ void device_unpackleftBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
      double* array,
const double* left_buffer,
const int depth)
{
    int x_extra = type.x_extra;
    int y_extra = type.y_extra;

    //__kernel_indexes;
    const int glob_id = threadIdx.x + blockIdx.x * blockDim.x;
    const int row = glob_id / depth;
    const int column = glob_id % depth;

    if (row >= (y_min + 1) - depth && row <= (y_max + 1) + y_extra + depth)
    {
        const int row_begin = row * (x_max + 4 + x_extra);

        array[row_begin + (x_min + 1) - (1 + column)] = left_buffer[VERT_IDX(column)];
    }
}

/************************************************************/

__global__ void device_packrightBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
const double* array,
      double* right_buffer,
const int depth)
{
    int x_extra = type.x_extra;
    int y_extra = type.y_extra;

    //__kernel_indexes;
    const int glob_id = threadIdx.x + blockIdx.x * blockDim.x;
    const int row = glob_id / depth;
    const int column = glob_id % depth;

    if (row >= (y_min + 1) - depth && row <= (y_max + 1) + y_extra + depth)
    {
        const int row_begin = row * (x_max + 4 + x_extra);

        right_buffer[VERT_IDX(column)] = array[row_begin + (x_max + 1) + 1 - (1 + column)];
    }
}

__global__ void device_unpackrightBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
      double* array,
const double* right_buffer,
const int depth)
{
    int x_extra = type.x_extra;
    int y_extra = type.y_extra;

    //__kernel_indexes;
    const int glob_id = threadIdx.x + blockIdx.x * blockDim.x;
    const int row = glob_id / depth;
    const int column = glob_id % depth;

    if (row >= (y_min + 1) - depth && row <= (y_max + 1) + y_extra + depth)
    {
        const int row_begin = row * (x_max + 4 + x_extra);

        array[row_begin + (x_max + 1) + x_extra + 1 + column] = right_buffer[VERT_IDX(column)];
    }
}

/************************************************************/

__global__ void device_packbottomBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
double* array,
double* bottom_buffer,
const int depth)
{
    int x_extra = type.x_extra;
    int y_extra = type.y_extra;

    __kernel_indexes;

    if (column >= (x_min + 1) - depth && column <= (x_max + 1) + x_extra + depth)
    {
        if (row < depth)
        {
            bottom_buffer[HORZ_IDX(row)] = array[THARR2D(0, (y_min + 1) + y_extra - 1 + row + 1, x_extra)];
        }
    }
}

__global__ void device_unpackbottomBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
double* array,
double* bottom_buffer,
const int depth)
{
    int x_extra = type.x_extra;

    __kernel_indexes;

    if (column >= (x_min + 1) - depth && column <= (x_max + 1) + x_extra + depth)
    {
        if (row < depth)
        {
            array[THARR2D(0, (y_min + 1) - (1 + 2*row), x_extra)] = bottom_buffer[HORZ_IDX(row)];
        }
    }
}

/************************************************************/

__global__ void device_packtopBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
double* array,
double* top_buffer,
const int depth)
{
    int x_extra = type.x_extra;

    __kernel_indexes;

    if (column >= (x_min + 1) - depth && column <= (x_max + 1) + x_extra + depth)
    {
        if (row < depth)
        {
            top_buffer[HORZ_IDX(row)] = array[THARR2D(0, (y_max + 1) + 1 - (1 + 2*row), x_extra)];
        }
    }
}

__global__ void device_unpacktopBuffer
(int x_min,int x_max,int y_min,int y_max,
cell_info_t type,
double* array,
double* top_buffer,
const int depth)
{
    int x_extra = type.x_extra;
    int y_extra = type.y_extra;

    __kernel_indexes;

    if (column >= (x_min + 1) - depth && column <= (x_max + 1) + x_extra + depth)
    {
        if (row < depth)
        {
            array[THARR2D(0, (y_max + 1) + y_extra + row + 1, x_extra)] = top_buffer[HORZ_IDX(row)];
        }
    }
}

