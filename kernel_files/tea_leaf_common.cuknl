/*
 *  Common kernels 
 */

__global__ void device_tea_leaf_finalise
(kernel_info_t kernel_info,
 const double * __restrict const density1,
 const double * __restrict const u1,
       double * __restrict const energy1)
{
    __kernel_indexes;

    if (WITHIN_BOUNDS)
    {
        energy1[THARR2D(0, 0, 0)] = u1[THARR2D(0, 0, 0)]/density1[THARR2D(0, 0, 0)];
    }
}

__global__ void device_tea_leaf_calc_residual
(kernel_info_t kernel_info,
 const double * __restrict const u,
 const double * __restrict const u0,
       double * __restrict const r,
 const double * __restrict const Kx,
 const double * __restrict const Ky)
{
    __kernel_indexes;

    if (WITHIN_BOUNDS)
    {
        const double smvp = (1.0
            + (Ky[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)])
            + (Kx[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]))*u[THARR2D(0, 0, 0)]
            - (Ky[THARR2D(0, 1, 0)]*u[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)]*u[THARR2D(0, -1, 0)])
            - (Kx[THARR2D(1, 0, 0)]*u[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]*u[THARR2D(-1, 0, 0)]);

        r[THARR2D(0, 0, 0)] = u0[THARR2D(0, 0, 0)] - smvp;
    }
}

__global__ void device_tea_leaf_calc_2norm
(kernel_info_t kernel_info,
 const double *            const arr1,
 const double *            const arr2,
       double * __restrict const rro)
{
    __kernel_indexes;

    __shared__ double rro_shared[BLOCK_SZ];
    rro_shared[lid] = 0.0;

    if (WITHIN_BOUNDS)
    {
        rro_shared[lid] = arr1[THARR2D(0, 0, 0)]*arr2[THARR2D(0, 0, 0)];
    }

    Reduce< double, BLOCK_SZ/2 >::run(rro_shared, rro, SUM);
}

__global__ void device_tea_leaf_init_common
(kernel_info_t kernel_info, 
 const double * __restrict const density, 
 const double * __restrict const energy,       
       double * __restrict const Kx,       
       double * __restrict const Ky, 
 double rx, double ry, const int coef)
{    
  __kernel_indexes;
    if (WITHIN_BOUNDS)    
    {        
      double dens_centre = 0;        
      double dens_left = 0;        
      double dens_down = 0;        
      if (COEF_CONDUCTIVITY == coef)        
      {            
        dens_centre = density[THARR2D(0, 0, 0)];            
        if (THARR2D(-1, 0, 0) > 0)
        {                
          dens_left = density[THARR2D(-1, 0, 0)];            
        }            
        if (THARR2D(0, -1, 0) > 0)
        {                
          dens_down = density[THARR2D(0, -1, 0)];            
        }        
      }        
      else if (COEF_RECIP_CONDUCTIVITY == coef)        
      {            
        dens_centre = 1.0/density[THARR2D(0, 0, 0)];            
        dens_left = 1.0/density[THARR2D(-1, 0, 0)];            
        dens_down = 1.0/density[THARR2D(0, -1, 0)];        
      }
        Kx[THARR2D(0, 0, 0)] = (dens_left + dens_centre)/(2.0*dens_left*dens_centre);        
        Kx[THARR2D(0, 0, 0)] *= rx;
        Ky[THARR2D(0, 0, 0)] = (dens_down + dens_centre)/(2.0*dens_down*dens_centre);        
        Ky[THARR2D(0, 0, 0)] *= ry;    
    }
}

__global__ void device_tea_leaf_zero_boundaries
(kernel_info_t kernel_info,
       double * __restrict const Kx,
       double * __restrict const Ky,
 int zero_left, int zero_right, int zero_bottom, int zero_top)
{
    __kernel_indexes;

    if (WITHIN_BOUNDS)
    {
        if (zero_left && column <= HALO_DEPTH)
        {
            Kx[THARR2D(0, 0, 0)] = 0;
        }

        if (zero_right && column > (x_max - 1) + HALO_DEPTH)
        {
            Kx[THARR2D(0, 0, 0)] = 0;
        }

        if (zero_bottom && row <= HALO_DEPTH)
        {
            Ky[THARR2D(0, 0, 0)] = 0;
        }

        if (zero_top && row > (y_max - 1) + HALO_DEPTH)
        {
            Ky[THARR2D(0, 0, 0)] = 0;
        }
    }
}

__global__ void device_tea_leaf_init_jac_diag
(kernel_info_t kernel_info,
       double * __restrict const Mi,
 const double * __restrict const Kx,
 const double * __restrict const Ky)
{
    __kernel_indexes;

    if (WITHIN_BOUNDS)
    {
        const double diag = (1.0
            + (Ky[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)])
            + (Kx[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]));

        Mi[THARR2D(0, 0, 0)] = 1.0/diag;
    }
}

