
__global__ void device_tea_leaf_finalise
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const density1,
 const double * __restrict const u1,
       double * __restrict const energy1)
{
    __kernel_indexes;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        energy1[THARR2D(0, 0, 0)] = u1[THARR2D(0, 0, 0)]/density1[THARR2D(0, 0, 0)];
    }
}

__global__ void device_tea_leaf_calc_residual
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const u,
 const double * __restrict const u0,
       double * __restrict const r,
 const double * __restrict const Kx,
 const double * __restrict const Ky)
{
    __kernel_indexes;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        const double smvp = (1.0
            + (Ky[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)])
            + (Kx[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]))*u[THARR2D(0, 0, 0)]
            - (Ky[THARR2D(0, 1, 0)]*u[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)]*u[THARR2D(0, -1, 0)])
            - (Kx[THARR2D(1, 0, 0)]*u[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]*u[THARR2D(-1, 0, 0)]);

        r[THARR2D(0, 0, 0)] = u0[THARR2D(0, 0, 0)] - smvp;
    }
}

__global__ void device_tea_leaf_common_calc_2norm
(int x_min, int x_max, int y_min, int y_max,
 const double *            const arr1,
 const double *            const arr2,
       double * __restrict const rro)
{
    __kernel_indexes;

    __shared__ double rro_shared[BLOCK_SZ];
    rro_shared[lid] = 0.0;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        rro_shared[lid] = arr1[THARR2D(0, 0, 0)]*arr2[THARR2D(0, 0, 0)];
    }

    Reduce< double, BLOCK_SZ/2 >::run(rro_shared, rro, SUM);
}
