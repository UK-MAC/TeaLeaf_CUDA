/*
 *  Kernels used for block jacobi preconditioner
 */

#define COEF_A (-Ky[THARR2D(0,k+ 0, 0)])
#define COEF_B (1.0 + (Ky[THARR2D(0,k+ 1, 0)] + Ky[THARR2D(0,k+ 0, 0)]) + (Kx[THARR2D(1,k+ 0, 0)] + Kx[THARR2D(0,k+ 0, 0)]))
#define COEF_C (-Ky[THARR2D(0,k+ 1, 0)])

#define BLOCK_TOP 4

__device__ void block_solve_func
(kernel_info_t kernel_info,
 const double r_local[BLOCK_SZ],
       double z_local[BLOCK_SZ],
 const double * __restrict const cp,
 const double * __restrict const bfp,
 const double * __restrict const Kx,
 const double * __restrict const Ky)
{
    __kernel_indexes;

    const size_t loc_row_size = LOCAL_X;

    const size_t upper_limit = BLOCK_TOP;

    int k = 0;
#define LOC_K (loc_column + k*loc_row_size)

    double dp_priv[JACOBI_BLOCK_SIZE];

    dp_priv[k] = r_local[LOC_K]/COEF_B;

    for (k = 1; k < upper_limit; k++)
    {
        dp_priv[k] = (r_local[LOC_K] - COEF_A*dp_priv[k - 1])*bfp[THARR2D(0, k, 0)];
    }

    k = upper_limit - 1;

    z_local[LOC_K] = dp_priv[k];

    for (k = upper_limit - 2; k >= 0; k--)
    {
        z_local[LOC_K] = dp_priv[k] - cp[THARR2D(0, k, 0)]*z_local[LOC_K + LOCAL_X];
    }
}

__global__ void device_tea_leaf_block_solve
(kernel_info_t kernel_info,
 const double * __restrict const r,
       double * __restrict const z,
 const double * __restrict const cp,
 const double * __restrict const bfp,
 const double * __restrict const Kx,
 const double * __restrict const Ky)
{
    __kernel_indexes;

    __shared__ double r_l[BLOCK_SZ];
    __shared__ double z_l[BLOCK_SZ];

    r_l[lid] = 0;
    z_l[lid] = 0;

    if (WITHIN_BOUNDS)
    {
        r_l[lid] = r[THARR2D(0, 0, 0)];
    }

    __syncthreads();
    if (loc_row == 0)
    {
        block_solve_func(kernel_info, r_l, z_l, cp, bfp, Kx, Ky);
    }
    __syncthreads();

    if (WITHIN_BOUNDS)
    {
        z[THARR2D(0, 0, 0)] = z_l[lid];
    }
}

__global__ void device_tea_leaf_block_init
(kernel_info_t kernel_info,
 const double * __restrict const r,
 const double * __restrict const z,
       double * __restrict const cp,
       double * __restrict const bfp,
 const double * __restrict const Kx,
 const double * __restrict const Ky)
{
    __kernel_indexes;

    const size_t upper_limit = BLOCK_TOP;

    if (WITHIN_BOUNDS)
    {
        if (loc_row == 0)
        {
            int k = 0;

            cp[THARR2D(0, k, 0)] = COEF_C/COEF_B;

            for (k = 1; k < upper_limit; k++)
            {
                bfp[THARR2D(0, k, 0)] = 1.0/(COEF_B - COEF_A*cp[THARR2D(0, k - 1, 0)]);
                cp[THARR2D(0, k, 0)] = COEF_C*bfp[THARR2D(0, k, 0)];
            }
        }
    }
}

