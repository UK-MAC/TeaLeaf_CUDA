
__global__ void device_tea_leaf_cheby_solve_init_p
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const u,
 const double * __restrict const u0,
       double * __restrict const p,
       double * __restrict const r,
       double * __restrict const Mi,
       double * __restrict const w,
 const double * __restrict const Kx,
 const double * __restrict const Ky,
 double theta, double rx, double ry)
{
    __kernel_indexes;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        w[THARR2D(0, 0, 0)] = (1.0
            + (Ky[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)])
            + (Kx[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]))*u[THARR2D(0, 0, 0)]
            - (Ky[THARR2D(0, 1, 0)]*u[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)]*u[THARR2D(0, -1, 0)])
            - (Kx[THARR2D(1, 0, 0)]*u[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]*u[THARR2D(-1, 0, 0)]);

        r[THARR2D(0, 0, 0)] = u0[THARR2D(0, 0, 0)] - w[THARR2D(0, 0, 0)];
        p[THARR2D(0, 0, 0)] = (Mi[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)])/theta;
    }
}

__global__ void device_tea_leaf_cheby_solve_calc_u
(int x_min, int x_max, int y_min, int y_max,
       double * __restrict const u,
 const double * __restrict const p)
{
    __kernel_indexes;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        u[THARR2D(0, 0, 0)] += p[THARR2D(0, 0, 0)];
    }
}

__global__ void device_tea_leaf_cheby_solve_calc_p
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const u,
 const double * __restrict const u0,
       double * __restrict const p,
       double * __restrict const r,
       double * __restrict const Mi,
       double * __restrict const w,
 const double * __restrict const Kx,
 const double * __restrict const Ky,
 const double * __restrict const alpha,
 const double * __restrict const beta,
 double rx, double ry,
 int step)
{
    __kernel_indexes;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        w[THARR2D(0, 0, 0)] = (1.0
            + (Ky[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)])
            + (Kx[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]))*u[THARR2D(0, 0, 0)]
            - (Ky[THARR2D(0, 1, 0)]*u[THARR2D(0, 1, 0)] + Ky[THARR2D(0, 0, 0)]*u[THARR2D(0, -1, 0)])
            - (Kx[THARR2D(1, 0, 0)]*u[THARR2D(1, 0, 0)] + Kx[THARR2D(0, 0, 0)]*u[THARR2D(-1, 0, 0)]);

        r[THARR2D(0, 0, 0)] = u0[THARR2D(0, 0, 0)] - w[THARR2D(0, 0, 0)];
        p[THARR2D(0, 0, 0)] = alpha[step]*p[THARR2D(0, 0, 0)]
                            + beta[step]*Mi[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)];
    }
}

__global__ void device_tea_leaf_cheby_solve_calc_resid
(int x_min, int x_max, int y_min, int y_max,
 const double * __restrict const r,
       double * __restrict const rro)
{
    __kernel_indexes;

    __shared__ double rro_shared[BLOCK_SZ];
    rro_shared[lid] = 0.0;

    if (row >= (y_min + 1) - 0 && row <= (y_max + 1) + 0
    && column >= (x_min + 1) - 0 && column <= (x_max + 1) + 0)
    {
        rro_shared[lid] = r[THARR2D(0, 0, 0)]*r[THARR2D(0, 0, 0)];
    }

    Reduce< double, BLOCK_SZ/2 >::run(rro_shared, rro, SUM);
}

